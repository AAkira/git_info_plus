group 'io.github.aakira.git_info_plus'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.3.50'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdkVersion 16

        buildConfigField "String", "GIT_BRANCH_NAME", "\"${getBranchName()}\""
        buildConfigField "String", "GIT_LAST_COMMIT_DATE", "${getLastCommitDate()}"
        buildConfigField "String", "GIT_LAST_COMMIT_HASH", "\"${getHash()}\""
        buildConfigField "String", "GIT_LAST_COMMIT_HASH_SHORT", "\"${getHashShort()}\""
        buildConfigField "String", "GIT_LAST_COMMIT_MESSAGE", "\"${getCommitMessage()}\""
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

def getBranchName() {
    def value = execCommand(['git', 'branch', '--show-current'], null)
    if (value == null) {
        return System.getenv("GIT_BRANCH_NAME")
    } else {
        return value
    }
}

def getLastCommitDate() {
    def value = execCommand(['git', '--no-pager', 'log', '-1', '--format="%ai"'], null)
    if (value == null) {
        return System.getenv("GIT_LAST_COMMIT_DATE")
    } else {
        return value
    }
}

def getHash() {
    def value = execCommand(['git', 'rev-parse', 'HEAD'], null)
    if (value == null) {
        return System.getenv("GIT_LAST_COMMIT_HASH")
    } else {
        return value
    }
}

def getHashShort() {
    def value = execCommand(['git', 'rev-parse', '--short', 'HEAD'], null)
    if (value == null) {
        return System.getenv("GIT_LAST_COMMIT_HASH_SHORT")
    } else {
        return value
    }
}

def getCommitMessage() {
    def value = execCommand(['git', 'log', '-1', '--pretty=%s'], null)
    if (value == null) {
        return System.getenv("GIT_LAST_COMMIT_MESSAGE")
    } else {
        return value
    }
}

def execCommand(command, returnValue) {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            workingDir buildDir.path
            commandLine command
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (e) {
        return returnValue
    }
}
